_ = require 'lodash'
React = require 'react'
H = React.DOM
R = React.createElement

formUtils = require './formUtils'
NumberAnswerComponent = require './answers/NumberAnswerComponent'

# Rosters are repeated information, such as asking questions about household members N times.
# A roster matrix is a list of columns with one row for each entry in the roster
# TODO add validation of columns
# TODO add required columns support
# TODO Add to schema
module.exports = class RosterMatrixComponent extends React.Component
  @contextTypes:
    locale: React.PropTypes.string

  # NOTE: I don't know much about RosterMatrixComponent yet, so this might all be wrong!!!
  # TODO: Need to check visibility of columns
  # this function will need to create an id that matches the id generated by the VisibilityEntity

  @propTypes:
    rosterMatrix: React.PropTypes.object.isRequired # Design of roster matrix. See schema
    data: React.PropTypes.object      # Current data of response. 
    onDataChange: React.PropTypes.func.isRequired   # Called when data changes
    isVisible: React.PropTypes.func.isRequired # (id) tells if an item is visible or not

  # Get the current answer value
  getAnswer: ->
    # Prefer rosterId if specified, otherwise use id. Default to array
    return @props.data[@props.item.rosterId or @props.item._id] or []

  # Propagate an answer change to the onDataChange
  handleAnswerChange: (answer) =>
    # Prefer rosterId if specified, otherwise use id. Default to array
    change = {}
    change[@props.item.rosterId or @props.item._id] = answer
    @props.onDataChange(_.extend({}, @props.data, change))

  # Handles a change in data of a specific entry of the roster
  handleEntryDataChange: (index, data) =>
    answer = @getAnswer().slice()
    answer[index] = data
    @handleAnswerChange(answer)

  handleAdd: =>
    answer = @getAnswer().slice()
    answer.push({})
    @handleAnswerChange(answer)

  handleRemove: (index) =>
    answer = @getAnswer().slice()
    answer.splice(index, 1)
    @handleAnswerChange(answer)

  handleCellChange: (entryIndex, columnId, value) =>
    data = @getAnswer()[entryIndex]
    change = {}
    change[columnId] = value
    data = _.extend({}, data, change)

    @handleEntryDataChange(entryIndex, data)

  renderName: ->
    H.h3 key: "prompt",
      formUtils.localizeString(@props.rosterMatrix.name, @context.locale)

  renderColumnHeader: (column, index) ->
    H.th key: column._id,
      formUtils.localizeString(column.name, @context.locale)

      # Required star
      if column.required
        H.span className: "required", "*"

  renderHeader: ->
    H.thead null,
      H.tr null,
        _.map(@props.rosterMatrix.columns, (column, index) => @renderColumnHeader(column, index))
        # Extra for remove button
        if @props.rosterMatrix.allowRemove
          H.th(null)

  renderCell: (entry, entryIndex, column, columnIndex) ->
    value = @getAnswer()[entryIndex][column._id]

    # Create element
    switch column._type
      when "Text"
        elem = H.input type: "text", className: "form-control input-sm", value: value, onChange: (ev) => @handleCellChange(entryIndex, column._id, ev.target.value)
      when "Number"
        elem = R NumberAnswerComponent, small: true, style: { maxWidth: "10em"}, value: value, onChange: (val) => @handleCellChange(entryIndex, column._id, val)
      # TODO
      when "Checkbox"
        elem = H.div 
          className: "touch-checkbox #{if value then "checked" else ""}"
          onClick: => @handleCellChange(entryIndex, column._id, not value)
          style: { display: "inline-block" }, 
            "\u200B" # ZWSP
      when "Dropdown"
        elem = H.select 
          className: "form-control input-sm"
          style: { width: "auto" }
          value: value
          onChange: ((ev) => @handleCellChange(entryIndex, column._id, if ev.target.value then ev.target.value else null)),
            H.option key: "__none__", value: ""
            _.map column.choices, (choice) =>
              text = formUtils.localizeString(choice.label, @context.locale)
              return H.option key: choice.id, value: choice.id, text

    return H.td null, elem

  renderEntry: (entry, index) ->
    H.tr key: index,
      _.map @props.rosterMatrix.columns, (column, columnIndex) => @renderCell(entry, index, column, columnIndex)
      if @props.rosterMatrix.allowRemove
        H.td key: "_remove",
          H.button type: "button", className: "btn btn-sm btn-link", onClick: @handleRemove.bind(null, index),
            H.span className: "glyphicon glyphicon-remove"  

  renderAdd: ->
    if @props.rosterMatrix.allowAdd
      H.div key: "add", style: { marginTop: 10 },
        H.button type: "button", className: "btn btn-default btn-sm", onClick: @handleAdd,
          H.span className: "glyphicon glyphicon-plus"
          " " + T("Add")

  render: ->
    H.div style: { padding: 5, marginBottom: 20 },
      @renderName()
      H.table className: "table",
        @renderHeader()
        H.tbody null,
          _.map(@getAnswer(), (entry, index) => @renderEntry(entry, index))

      # Display message if none
      if @getAnswer().length == 0
        H.div(style: { paddingLeft: 20 }, H.i(null, T("None")))

      @renderAdd() 